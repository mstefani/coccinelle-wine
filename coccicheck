#!/usr/bin/perl -w
#
# coccicheck
#       A more generic coccinelle wrapper but following the same idea from the Linux version.
use strict;
use Getopt::Long qw(:config pass_through);

my $nrproc = `getconf _NPROCESSORS_ONLN`;
chomp($nrproc);
my $macros = $0;
$macros =~ s,/[^/]+$,/macros,;

my $report = 0;
my $tryreport = 0;
GetOptions(
        "report" => \$report,
        "try-report" => \$tryreport,
);

my $srcdir;
if ($ARGV[0] !~ /\.cocci$/) {
   $srcdir = $ARGV[0];
} else {
   $srcdir = $ARGV[1];
}
$srcdir =~ s,/\K[^/]+$,, if (! -d $srcdir);
$srcdir = `cd '$srcdir' && git rev-parse --show-toplevel`;
chomp($srcdir);

my @cocci = grep(/\.cocci$/, @ARGV);
if ($tryreport) {
    system('grep', '-q', '^virtual report', $cocci[0]);
    $report = 1 if ($? >> 8 == 0);
}
my $optstr = `grep -P '^\\s*//\\s*Options:' '$cocci[0]' | cut -d: -f2`;
$optstr =~ s/^\s+//;
$optstr =~ s/\s+$//;
my @opts = split(/\s+/, $optstr);

if ($report) {
    push(@opts, '--no-show-diff', '-D', 'report');
}

system('spatch', '--macro-file-builtins', $macros, '-j', $nrproc, '--patch', $srcdir, '--indent', 4,
       '--max-width', 100, '--quiet', @opts, @ARGV);
